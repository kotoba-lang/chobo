(ns chobo.ledger-test
  (:require [clojure.test :refer [deftest is testing]]
            [chobo.ledger :as l]))

(def act (l/activity {:lane :billing :kind :invoice :title "inv 1" :tenant "gftd"}))

(deftest append-and-query-test
  (let [lg (-> (l/ledger)
               (l/append-activity act)
               (l/append-activity (l/activity {:lane :sales :kind :order :title "o1" :tenant "gftd"}))
               (l/append-effect (l/effect {:activity (:id act) :kind :payment :risk :high})))]
    (is (= 2 (count (:activities lg))))
    (is (= 1 (count (l/activities-by-lane lg :billing))))
    (is (= 1 (count (l/activities-by-lane lg :sales))))
    (is (= 1 (count (l/effects-for-activity lg (:id act)))))
    (is (= 2 (count (l/activities-by-tenant lg "gftd"))))))

(deftest activity-transitions-test
  (is (l/can-transition? :open :done))
  (is (l/can-transition? :open :cancelled))
  (is (not (l/can-transition? :done :open)))
  (is (= :done (:state (l/transition-activity act :done))))
  (is (nil? (l/transition-activity act :cancelled-unreachable))))

(deftest effect-lifecycle-test
  (let [e (l/effect {:kind :payment :risk :high})]
    (is (= :proposed (:status e)))
    (is (= :approved (:status (l/approve-effect e))))
    (is (= :applied (:status (l/apply-effect (l/approve-effect e)))))
    (is (= :rejected (:status (l/reject-effect e))))
    (is (nil? (l/apply-effect e))))) ; can't apply a proposed effect

(deftest mock-store-test
  (let [s (l/mock-ledger-store)
        stored (l/put-record! s act)]
    (is (:id stored))
    (is (= (:id stored) (:id (l/get-activity s (:id stored)))))
    (is (= 1 (count (l/list-activities s))))))
