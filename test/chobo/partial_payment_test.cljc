(ns chobo.partial-payment-test
  "Partial payments + credit balance."
  (:require [clojure.test :refer [deftest is testing]]
            [chobo.invoice :as inv]))

(defn issued-inv
  "An issued invoice with the given line amount."
  [amount]
  (-> (inv/invoice "gftd")
      (inv/add-line (inv/line "Subscription" amount))
      (inv/mark-issued)))

(deftest outstanding-test
  (let [i (issued-inv 3000)]
    (is (= 3000 (:amount (inv/outstanding i)))))
  (let [i (-> (issued-inv 3000)
              (inv/apply-payment {:id "p1" :amount 1000 :at "2026-06-01"}))]
    (is (= 2000 (:amount (inv/outstanding i))))))

(deftest amount-paid-test
  (let [i (-> (issued-inv 3000)
              (inv/apply-payment {:id "p1" :amount 1000})
              (inv/apply-payment {:id "p2" :amount 500}))]
    (is (= 1500 (inv/amount-paid i)))))

(deftest apply-partial-payment-test
  (let [i (-> (issued-inv 3000)
              (inv/apply-payment {:id "p1" :amount 1000 :at "2026-06-01"}))]
    (is (= :issued (:status i)))                ; not fully paid yet
    (is (not (inv/fully-paid? i)))))

(deftest apply-full-payment-test
  (let [i (-> (issued-inv 3000)
              (inv/apply-payment {:id "p1" :amount 3000 :at "2026-06-01"}))]
    (is (= :paid (:status i)))
    (is (inv/fully-paid? i))))

(deftest credit-balance-test
  (let [i1 (-> (issued-inv 2000) (inv/apply-payment {:id "p1" :amount 3000}))  ; overpay 1000
        i2 (-> (issued-inv 5000) (inv/apply-payment {:id "p2" :amount 5000}))] ; exact
    (is (= 1000 (inv/credit-balance [i1 i2]))))) ; only i1 overpaid
